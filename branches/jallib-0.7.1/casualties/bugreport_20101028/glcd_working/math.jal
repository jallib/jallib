-- ----------------------------------------------------------------------
-- Title: math.jal - Collection of mathematical functions.
-- Author: Rob Hamerling, Michael Watterson Copyright (c) 2009, all rights reserved.
-- Adapted-by:  Michael Watterson, Eur van Andel
-- Compiler: 2.4l
-- Revision: $Revision$
--
-- This file is part of jallib  (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description:
-- Collection of mathematical routines.
--
-- Sources: Several authors.
--


-- --------------------------------------------------------
-- Calculate the square root of an unsigned 16-bits integer
-- Returns an 8-bits integer
-- Original author: Kyle York
-- --
function sqrt16(word in x) return byte is
   var word m, y
   
   m = 0x4000
   y = 0
   while (m != 0) loop
      var word b
      
      b = y | m
      y = y >> 1
      if (x >= b) then
         x = x - b
         y = y | m
      end if
      m = m >> 2
   end loop
   return byte(y)
end function


-- --------------------------------------------------------
-- Calculate the square root of an unsigned 32-bits integer
-- Returns a 16-bits integer
-- Original author: Kyle York
-- --
function sqrt32(dword in x) return word is
   var dword m, y
   
   m = 0x40000000
   y = 0
   while (m != 0) loop
      var dword b
      
      b = y | m
      y = y >> 1
      if (x >= b) then
         x = x - b
         y = y | m
      end if
      m = m >> 2
   end loop
   return word(y)
end function


-- Author: Michael Watterson

function digits_word(word in valu)return byte is
   var byte temp = 1
   var dword index = 9
   while (dword(valu) > index) loop
      index = index * 10 +9
      temp = temp +1
   end loop
   return temp
end function

-- fixed point math is 8 bits for decimal in this library.
--    approx two decimal paces, the part after "point" is 0 to 255
--    sword fixed point numbers are thus -127.00 to +127.00 approx
--    if you want bigger numbers replace all swords by sdwords and
--         sdwords by sbyte*8 if the compiler can do the math still.
function byte_to_fixed(byte in int) return sword is
   return ((sword(int)) << 8)
end function

function trunc_fixed(sword in fixed_point) return sbyte  is
   return ( sbyte(fixed_point >> 8))
end function

function round_fixed(sword in fixed_point) return sbyte  is
   if ((fixed_point & 0x10) >0)  then    -- 128 and more = 0.5
      return ( sbyte(fixed_point >> 8) +1)
   else
      return ( sbyte(fixed_point >> 8))
   end  if
end function

-- unlike integer max is 128 * 128  *256 approx
function mult_fixed(sword in a, sword in b) return sdword is
   return ((sdword(a) * sdword(b)) >> 8) -- fix point
end function

function div_fixed(sword in a, sword in b) return sdword  is
   return ( sword((sdword(a) * sdword (256*256)) / sdword(b)) >> 16)
end function

-- probably a better way to do this!
function mod_fixed(sword in a, sword in b) return sword is
   return ( a - sword(sdword(b)*((sdword(a) * sdword (256*256)) / sdword(b)) >> 16))
end function

-- sin values scaled to (256 * 1.0) -1 for values above Zero
-- (i.e. add one to lookup if using table directly for other than Zeroth Element)
-- apart from zero, all values are 1 less than actual
-- to allow storage in 1 byte each
const byte SINLOOKUP[] = {0, 3, 8, 12, 17, 21, 26, 30, 35, 39, 43, 48, 52, 57,
61, 65, 70, 74, 78, 82, 87, 91, 95, 99, 103, 107, 111, 115, 119, 123, 127,
131, 135, 138, 142, 146, 149, 153, 157, 160, 164, 167, 170, 174, 177, 180,
183, 186, 189, 192, 195, 198, 201, 203, 206, 209, 211, 214, 216, 218, 221,
223, 225, 227, 229, 231, 233, 235, 236, 238, 240, 241, 242, 244, 245, 246,
247, 248, 249, 250, 251, 252, 253, 253, 254, 254, 254, 255, 255, 255, 255 }
-- 0 to 90 degrees in 1 degree steps

-- 8bit resolution sin function R x Sin (angle) in degrees -32767 to +32767
-- result can be same magnitude and opposite sign

-- use to generate audio waves by changing angle at a fixed speed.
-- if using all 360 degrees in one degree steps, then frequency is step rate / 360
-- at nyquist frequency you only need 0 and 180 degrees, i.e. 1/2 sample rate.
-- if sample rate is 48kHz then for 12kHz you have only 4 samples, i.e. 90 degree steps
-- at 1.2kHz thus you use 9 degree steps.
-- 1 degree steps is thus  133.33  Hz if step speed is 48KHz
-- PWM can be loaded via this lookup
-- use 128 and add 127 to answer for 8 bit PWM
-- use 512 and add 511 to answer for 10bit PWM
-- if summing two frequencies, peak value is twice, so add as swords < 16383 and
-- divide sum by 2!
-- Author: Michael Watterson

-- works with fixed point numbers 256 = 1.0
function rsin8( sword in radius, sword in angle ) return sword is
   var sword ypos
   --make sure works for ANY angle ...
   while (angle < 0)   loop
      angle = angle + 360
   end loop
   while (angle >= 360)   loop
      angle = angle - 360
   end loop
   case angle of
      0:  ypos = 0
      90: ypos = radius
      180: ypos =0
      270: ypos = -radius
      otherwise
      block
         -- add or subtract one as lookups above zero have -1 so 256x 1.0
         -- fits in one byte
         if (angle > 0) & (angle < 90) then
            ypos = sword(SINLOOKUP[angle]) + 1
         elsif (angle > 90) & (angle < 180) then
            angle = angle - 90
            ypos = sword(SINLOOKUP[(90 - angle)]) + 1
         elsif (angle > 180) & (angle < 270) then
            angle = angle - 180
            ypos = -sword(SINLOOKUP[angle]) - 1
         elsif (angle > 270) & (angle < 360) then
            angle = angle - 270
            ypos = -sword(SINLOOKUP[(90 - angle)]) - 1
         end if
         ypos = ypos * radius / 256
      end block
   end case
   return (ypos)
end function

-- Author: Michael Watterson
function rcos8( sword in radius, sword in angle ) return sword is
   var sword xpos
   --make sure works for ANY angle ...
   while (angle < 0)   loop
      angle = angle + 360
   end loop
   while (angle >= 360)   loop
      angle = angle - 360
   end loop
   case angle of
      0:  xpos = radius
      90: xpos = 0
      180: xpos = -radius
      270: xpos = 0
      otherwise
      block
         -- add or subtract one as lookups above zero have -1 so 256x 1.0
         -- fits in one byte
         if (angle > 0) & (angle < 90) then
            xpos = sword(SINLOOKUP[(90 - angle)]) + 1
         elsif (angle > 90) & (angle < 180) then
            angle = angle - 90
            xpos = -sword(SINLOOKUP[angle]) - 1
         elsif (angle > 180) & (angle < 270) then
            angle = angle - 180
            xpos = -sword(SINLOOKUP[(90 - angle)]) - 1
         elsif (angle > 270) & (angle < 360) then
            angle = angle - 270
            xpos = sword(SINLOOKUP[angle]) + 1
         end if
         xpos = xpos * radius / 256
      end block
   end case
   return (xpos)
end function

-- use to rotate angle of a line by calculating endpoints
-- use to generate quadrature waves by changing angle at a fixed speed.
-- if using all 360 degrees in one degree steps, then frequency is step rate / 360
-- Author: Michael Watterson
procedure polar_to_cartesian(sword in radius, sword in angle, sword out xpos, sword out ypos) is
   -- make sure works for ANY angle ...
   while (angle < 0)   loop
      angle = angle + 360
   end loop
   while (angle >= 360)   loop
      angle = angle - 360
   end loop
   case angle of
      0:
      block
         xpos = radius
         ypos = 0
      end block
      90:
      block
         xpos = 0
         ypos = radius
      end block
      180:
      block
         xpos = -radius
         ypos = 0
      end block
      270:
      block
         xpos = 0
         ypos = -radius
      end block
      otherwise
      block
         -- add or subtract one as lookups above zero have -1 so 256 x 1.0
         -- fits in one byte
         if (angle > 0) & (angle < 90) then
            ypos = sword(SINLOOKUP[angle]) + 1
            xpos = sword(SINLOOKUP[(90 - angle)]) + 1
         elsif (angle > 90) & (angle < 180) then
            angle = angle - 90
            xpos = -sword(SINLOOKUP[angle]) - 1
            ypos = sword(SINLOOKUP[(90 - angle)]) + 1
         elsif (angle > 180) & (angle < 270) then
            angle = angle - 180
            xpos = -sword(SINLOOKUP[(90 - angle)]) - 1
            ypos = -sword(SINLOOKUP[angle]) - 1
         elsif (angle > 270) & (angle < 360) then
            angle = angle - 270
            xpos = sword(SINLOOKUP[angle]) + 1
            ypos = -sword(SINLOOKUP[(90 - angle)]) - 1
         end if
         xpos = xpos * radius / 256
         ypos = ypos * radius / 256
      end block
   end case
end procedure

-- Author: Eur van Andel, eur@fiwihex.nl
function abs8(sbyte in x) return byte is
   if x < 0 then
      return byte(-x)
   else 
      return byte(x)
   end if
end function

function abs16(sword in x) return word is
   if x < 0 then
      return word(-x)
   else 
      return word(x)
   end if
end function

function abs32(sdword in x) return dword is
   if x < 0 then
      return dword(-x)
   else 
      return dword(x)
   end if
end function


function sign8(sbyte in x) return sbyte is
   if x < 0 then
      return -1
   elsif x == 0 then
      return 0
   else 
      return 1
   end if
end function

function sign16(sword in x) return sbyte is
   if x < 0 then
      return -1
   elsif x == 0 then
         return 0
   else 
      return 1
   end if
end function

function sign32(sdword in x) return sbyte is
   if x < 0 then
      return -1
   elsif x == 0 then 
         return 0
   else 
      return 1
   end if
end function




