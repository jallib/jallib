-- Title: i2c_hardware
-- Author: Stef Mientki, Copyright (C) 2005 Stef Mientki
-- Adapted-by: Joep Suijs, Albert Faber
-- Compiler: >=2.4g
-- Revision: $Revision$
--
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: Hardware implementation of I2C (single) master.
-- --
-- -- Define i2c pins like:
-- alias i2c_scl            is pin_c3  ; optional, but good habit ;)
-- alias i2c_scl_direction  is pin_c3_direction
-- alias i2c_sda            is pin_c4  ; optional, but good habit ;)
-- alias i2c_sda_direction  is pin_c4_direction
-- --
-- const word _i2c_bus_speed = 1 ; * 100kHz
-- const bit _i2c_level = true   ; i2c levels (not SMB)
--
-- TODO:
--

-- --------------------------------------------------------------------
-- some constants needed for the baudrate generator
-- --------------------------------------------------------------------
const _i2c_100khz = (target_clock / (4 *  100_000)) - 1
const _i2c_400khz = (target_clock / (4 *  400_000)) - 1
const _i2c_1mhz   = (target_clock / (4 * 1000_000)) - 1
-- --------------------------------------------------------------------

-- --------------------------------------------------------------------
-- if the device has changed, change the speed if necessary
-- --------------------------------------------------------------------
procedure i2c_initialize() is

   -- One time initialize ---------------------------------------------
   -- i2c requires to set TRIS bits to input
   -- so to be sure ...
   pin_SCL_direction = input
   pin_SDA_direction = input

   sspcon  = 0b0010_1000   -- set up 16f87x as master device
   sspcon2 = 0b0010_0000   -- sets default acknowledge bit value
   -- -----------------------------------------------------------------

   -- set correct speed
   if    _i2c_bus_speed == 10 then sspadd = _i2c_1mhz
   elsif _i2c_bus_speed == 4  then sspadd = _i2c_400khz
   else                            sspadd = _i2c_100khz
   end if

   -- set correct level
   -- SMP-bit7 = 0, slew rate enabled, better for EMC
   if _i2c_level then
      sspstat = 0b_0000_0000   ;i2c bus levels, CKE=0
   else
      sspstat = 0b_0100_0000   ;SMbus levels, CKE=1
   end if
end procedure
-- -----------------------------------------------------------------------------


-- -----------------------------------------------------------------------------
-- i2c_start - Sends Start bit and waits untill finished
-- -----------------------------------------------------------------------------
procedure i2c_start() is
   SSPCON2_SEN = high
   while SSPCON2_SEN == high loop end loop
end procedure
-- -----------------------------------------------------------------------------

-- -----------------------------------------------------------------------------
-- i2c_restart - Sends Restart bit and waits untill finished
-- -----------------------------------------------------------------------------
procedure i2c_restart() is
   SSPCON2_RSEN = high
   while SSPCON2_RSEN == high loop end loop
end procedure
-- -----------------------------------------------------------------------------

-- -----------------------------------------------------------------------------
-- i2c_stop - Sends Stop bit and waits until finished
-- -----------------------------------------------------------------------------
procedure i2c_stop() is
   SSPCON2_PEN = high
   while SSPCON2_PEN == high loop end loop
end procedure
-- -----------------------------------------------------------------------------

-- -----------------------------------------------------------------------------
-- i2c_transmit_byte -
-- -----------------------------------------------------------------------------
-- -----------------------------------------------------------------------------
function i2c_transmit_byte(byte in data) return bit is

   PIR1_SSPIF = false  ; clear pending flag
   sspbuf = data       ; write data

   -- wait untill write is finished
   while ! PIR1_SSPIF loop end loop

   -- get Acknowledge Status Bit _ACKSTAT
   -- wich indicates that the slave has responded (or not)
   -- if i2c device send an Aknowledge, then ready and OK
   if SSPCON2_ACKSTAT == low  then
      return true -- okay
   else
      sspcon_sspen = false;
      sspcon_sspen = true;
      
      return false -- no response
   end if
end function
-- -----------------------------------------------------------------------------

-- -----------------------------------------------------------------------------
-- i2c_receive_byte -
-- -----------------------------------------------------------------------------
-- start receiving of a byte and waits till finished
-- if param ACK is true, the byte is acknowledged and next bytes can be received.
-- if param ACK is false, the byte is nacked and a stop *should be* sent.
-- note: this behavior is inverted from the line level and orignal library, but
--       consistent with the i2c_software library.
-- -----------------------------------------------------------------------------
function i2c_receive_byte(bit in ACK ) return byte is
   var byte data
   -- start receive cyclus and wait till full byte received
   SSPCON2_RCEN = high
   ; delay_10us(10)
   while SSPSTAT_BF == low loop  end loop

   -- send Acknowledge (=low) if STOP=true=high
   -- and wait till finishd
   SSPCON2_ACKDT = ! ACK
   SSPCON2_ACKEN = high
   while SSPCON2_ACKEN == high loop end loop

   -- get read data
   data = sspbuf
   ;serial_hw_write(data)

   ;  if ACK == i2c_NACK then _i2c_stopbit end if
   return data
end function
-- -----------------------------------------------------------------------------





