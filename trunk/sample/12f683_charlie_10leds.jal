-- ------------------------------------------------------
-- Title: Control 10 LEDs with 4 I/O pins
--
-- Author: Rob Hamerling, Copyright (c) 2014..2014, all rights reserved.
--
-- Adapted-by:
--
-- Revision: $Revision$
--
-- Compiler: 2.4q3
--
-- This file is part of jallib  (http://jallib.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description:
-- Control a row of 10 charlieplexed leds, controlled with 4 pins.
-- In this example the 10 leds show the value of a counter in binary format.
--
-- This file has been generated by hand (manually maintained)
--
-- Sources:
-- - Microchip Application note TB029.
--
-- Notes:
--
-- ------------------------------------------------------
--
include 12f683                                  -- target PICmicro
--
pragma target clock    8_000_000                -- oscillator frequency

-- configuration memory settings (fuses)
pragma target OSC      INTOSC_NOCLKOUT          -- internal oscillator
pragma target WDT      disabled                 -- watchdog software controlled
pragma target PWRTE    enabled                  -- power up delay
pragma target MCLR     internal                 -- make MCLR pin available I/O
pragma target BROWNOUT disabled                 -- no brownout
pragma target IESO     disabled                 -- no osc switch over
pragma target FCMEN    disabled                 -- no clock monitoring

OSCCON_IRCF = 0b111                             -- 8 MHz
OSCCON_SCS  = 0b00                              -- clock determined by fuses

enable_digital_io()                             -- disable analog I/O

include delay

-- --- setup for charlie library -----------------------------
--
const CP_COLS = 10                              -- 10 leds in a row
const CP_ROWS = 1                               -- single row
const CP_FREQ = 40                              -- refresh frequency (Hz)

-- --- led array connections ---------------------------------
-- Per led - upper nibble: source pin number
--         - lower nibble: sink pin number
-- Used pins: pin_A0,A1,A4,A5
--                                                            source  sink
const byte CP_XY[CP_COLS * CP_ROWS] = { 0x_4_1,           --    4      1
                                        0x_1_4,           --    1      4
                                        0x_4_0,           --    4      0
                                        0x_0_4,           --    0      4
                                        0x_5_1,           --    5      1
                                        0x_1_5,           --    1      5
                                        0x_5_0,           --    5      0
                                        0x_0_5,           --    0      5
                                        0x_5_4,           --    5      4
                                        0x_4_5            --    4      5
                                      }

-- --- led pattern (changed in mainline) ---------------------

var word cp_pattern[CP_ROWS] = { 0 }            -- single row

-- --- ports used by this board ------------------------------

alias cp_port1 is portA
alias cp_port1_direction is portA_direction

include charlie

charlie_init()

-- --- main line --------------------------

forever loop

   cp_pattern[0] = 0
   for 1024 loop                                -- 2^10
      delay_1ms(100)
      cp_pattern[0] = cp_pattern[0] + 1         -- binary counter
   end loop

end loop

