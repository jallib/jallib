-- ------------------------------------------------------
-- Title: Control 12 LEDs with 4 I/O pins
--
-- Author: Rob Hamerling, Copyright (c) 2014..2014, all rights reserved.
--
-- Adapted-by:
--
-- Revision: $Revision$
--
-- Compiler: 2.4q3
--
-- This file is part of jallib  (http://jallib.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description:
-- Control a row of 12 charlieplexed leds, controlled with 4 pins.
-- In this example the 12 leds show the value of a counter in binary format
--
-- This file has been generated by hand (manually maintained)
--
-- Sources:
--
-- Notes:
--
-- ------------------------------------------------------
--
include 12f1840                                 -- target PICmicro
--
pragma target clock    32_000_000               -- oscillator frequency

-- configuration memory settings (fuses)
pragma target OSC      INTOSC_NOCLKOUT          -- Internal oscillator
pragma target WDT      disabled                 -- no watchdog
pragma target PWRTE    enabled                  -- power up delay
pragma target MCLR     internal                 -- make MCLR pin available I/O
pragma target BROWNOUT disabled                 -- no brownout
pragma target CLKOUTEN disabled                 -- no clock out
pragma target FCMEN    disabled                 -- no clock monitoring
pragma target PLLEN    enabled                  -- PLL fixed on
pragma target LVP      disabled                 -- no LVP

OSCCON_IRCF = 0b1110                            -- 8 MHz (with PLL -> 32 MHz)
OSCCON_SCS  = 0b00                              -- clock determined by fuses

enable_digital_io()                             -- disable analog I/O

include delay

-- --- setup for charlie library -----------------------------
--
const CP_COLS = 12                              -- 12 leds per row
const CP_ROWS = 1                               -- 1 row
const CP_FREQ = 48                              -- refresh frequency

-- --- led array connections ---------------------------------
-- Per led - upper nibble: source pin number
--         - lower nibble: sink pin number
-- Used pins: pin_A0,A1,A4,A5
--                                                          source  sink
const byte CP_XY[CP_COLS * CP_ROWS] = { (1 << 4) | 0,   --    1      0
                                        (0 << 4) | 1,   --    0      1
                                        (5 << 4) | 0,   --    5      0
                                        (0 << 4) | 5,   --    0      5
                                        (4 << 4) | 0,   --    2      0
                                        (0 << 4) | 4,   --    0      4
                                        (5 << 4) | 1,   --    5      1
                                        (1 << 4) | 5,   --    1      5
                                        (1 << 4) | 4,   --    1      4
                                        (4 << 4) | 1,   --    4      1
                                        (5 << 4) | 4,   --    5      4
                                        (4 << 4) | 5    --    4      5
                                      }

-- --- led pattern (changed in mainline) ---------------------

var word cp_pattern[CP_ROWS] = { 0 }            -- single row

-- --- ports used by this board ------------------------------

alias cp_port1 is portA
alias cp_port1_direction is portA_direction

include charlie

charlie_init()

-- --- main line --------------------------

forever loop

   cp_pattern[0] = 0
   for 4096 loop                                -- full cycle (2^12)
      delay_1ms(100)                            -- cycles per second
      cp_pattern[0] = cp_pattern[0] + 1         -- modify led pattern
   end loop

end loop

