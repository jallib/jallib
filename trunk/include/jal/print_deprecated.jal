-- Title: Writes variables to output device
-- Author: Joep Suijs, Copyright (c) 2007..2008, all rights reserved.
-- Adapted-by: Joep Suijs, Michael Watterson
-- Compiler: >=2.2
-- Revision: $Revision: 2760 $
-- 
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Sources: 
-- 
-- Description: Outputs variables to output device.
-- formats: _decimal and _hex
-- vars:    bit, byte, sbyte, word, sword, dword, sdword
-- --  
-- Example:
-- --     
-- var sword bhl = -684
-- --     
-- ; print the signed word to the LCD display
-- print_sword_dec(lcd_char, bhl)
-- --     
-- ; and now print the same signed word to the serial port
-- print_sword_dec(serial_hw_data, bhl)
-- --
-- ; Specials:
-- print_crlf(serial_hw_data)  -- print cr + lf to device
-- ; 
-- const byte str1[] = "JalLibWorld V1.0\n"     
-- print_string(serial_hw_data, str1) -- print string to device
-- ;
-- ; Note - the whole string - that is the length of the array - 
-- ; is printed. When you use a variable array to construct strings,
-- ; you might want to terminate at a termination character, like 0.
-- ; You can set this termination character with:
-- ; const print_string_terminator = 255
-- ; (or use a var if you want to change it runtime.)
-- ;
-- include print      
--
-- Notes: 
--  print_sword_fp_dec uses "fixed point" integer, -127.256 to +127.256
--  Imagine the decimal. Thus 0x110 = 1.5 as 128 = 0.5 and 256 = 1
--

include jascii         
         
; prototypes
procedure _print_universal_dec(volatile byte out device, dword in data, sbyte in divisor_index)
procedure _print_suniversal_dec(volatile byte out device, sdword in data, sbyte in divisor_index)
procedure print_byte_binary(volatile byte out device, byte in data)

const byte nibble2hex[] = "0123456789ABCDEF"   -- conversion string

var bit print_prefix = false        

procedure print_crlf(volatile byte out device) is
   device = ASCII_CR -- cariage return
   device = ASCII_LF -- line feed
end procedure

procedure print_string(volatile byte out device, byte in str[]) is
   var word len = count(str)
   var byte i
   
   for len using i loop           
      if (defined(print_string_terminator) == true) then
         if (str[i] == print_string_terminator) then exit loop end if
      end if         
      device = str[i]
   end loop
      
end procedure

procedure print_bit_truefalse(volatile byte out device, bit in data) is
   
   const byte str1[] = "true"   
   const byte str0[] = "false"   

   if (data) then
      print_string(device, str1)
   else
      print_string(device, str0)
   end if
         
end procedure

procedure print_bit_highlow(volatile byte out device, bit in data) is
   
   const byte str1[] = "high"   
   const byte str0[] = "low"   

   if (data) then
      print_string(device, str1)
   else
      print_string(device, str0)
   end if
         
end procedure

procedure print_bit_10(volatile byte out device, bit in data) is
   
   if (data) then
      device = "1"
   else
      device = "0"
   end if
         
end procedure

procedure print_dword_binary(volatile byte out device, dword in data) is
   
   if (print_prefix) then
      device = "0"
      device = "b"
   end if

   print_byte_binary(device, byte(data>>24))
   print_byte_binary(device, byte(data>>16))
   print_byte_binary(device, byte(data>>8))
   print_byte_binary(device, byte(data))
   
end procedure

procedure print_word_binary(volatile byte out device, word in data) is

   if (print_prefix) then
      device = "0"
      device = "b"
   end if
   
   print_byte_binary(device, byte(data>>8))
   print_byte_binary(device, byte(data))
   
end procedure


procedure print_byte_binary(volatile byte out device, byte in data) is
   
   if (print_prefix) then
      device = "0"
      device = "b"
   end if

   for 8 loop    
      if ((data & 0x80) != 0) then
         device = "1"
      else
         device = "0"
      end if
      data = data * 2
   end loop
   
end procedure


procedure print_nibble_binary(volatile byte out device, byte in data) is
   
   for 4 loop    
      if ((data & 0x08) != 0) then
         device = "1"
      else
         device = "0"
      end if
      data = data << 1
   end loop
   
end procedure



procedure print_dword_hex(volatile byte out device, dword in data) is

   if (print_prefix) then
      device = "0"
      device = "x"
   end if
      
   device = nibble2hex[0x0F & (data>>28)]
   device = nibble2hex[0x0F & (data>>24)]
   device = nibble2hex[0x0F & (data>>20)]
   device = nibble2hex[0x0F & (data>>16)]
   device = nibble2hex[0x0F & (data>>12)]
   device = nibble2hex[0x0F & (data>>8)]
   device = nibble2hex[0x0F & (data>>4)]
   device = nibble2hex[0x0F & (data)]
   
end procedure

procedure print_sdword_hex(volatile byte out device, sdword in data) is

   if (print_prefix) then
      device = "0"
      device = "x"
   end if
      
   device = nibble2hex[0x0F & (data>>28)]
   device = nibble2hex[0x0F & (data>>24)]
   device = nibble2hex[0x0F & (data>>20)]
   device = nibble2hex[0x0F & (data>>16)]
   device = nibble2hex[0x0F & (data>>12)]
   device = nibble2hex[0x0F & (data>>8)]
   device = nibble2hex[0x0F & (data>>4)]
   device = nibble2hex[0x0F & (data)]
   
end procedure


procedure print_word_hex(volatile byte out device, word in data) is

   if (print_prefix) then
      device = "0"
      device = "x"
   end if
   
   device = nibble2hex[0x0F & (data>>12)]
   device = nibble2hex[0x0F & (data>>8)]
   device = nibble2hex[0x0F & (data>>4)]
   device = nibble2hex[0x0F & (data)]
   
end procedure


procedure print_sword_hex(volatile byte out device, sword in data) is

   if (print_prefix) then
      device = "0"
      device = "x"
   end if
   
   device = nibble2hex[0x0F & (data>>12)]
   device = nibble2hex[0x0F & (data>>8)]
   device = nibble2hex[0x0F & (data>>4)]
   device = nibble2hex[0x0F & (data)]
   
end procedure




procedure print_byte_hex(volatile byte out device, byte in data) is             

   if (print_prefix) then
      device = "0"
      device = "x"
   end if

   device = nibble2hex[0x0F & (data>>4)]
   device = nibble2hex[0x0F & (data)]
   
end procedure

procedure print_sdword_dec(volatile byte out device, sdword in data) is

   _print_suniversal_dec(device, data, 9)

end procedure

procedure print_sword_dec(volatile byte out device, sword in data) is

   _print_suniversal_dec(device, data, 4)

end procedure

-- 8 bit fixed point integer math
procedure print_sword_fp_dec(volatile byte out device, sword in data) is

   _print_suniversal_dec(device, (data >> 8), 4) -- whole part
   device = "."
   _print_suniversal_dec(device, (data & 0xFF), 4) -- decimal part
end procedure

procedure print_sbyte_dec(volatile byte out device, sbyte in data) is

   _print_suniversal_dec(device, data, 2)
      
end procedure

procedure print_dword_dec(volatile byte out device, dword in data) is

   _print_universal_dec(device, data, 9)

end procedure

procedure print_word_dec(volatile byte out device, word in data) is

   _print_universal_dec(device, data, 4)

end procedure

procedure print_byte_dec(volatile byte out device, byte in data) is

   _print_universal_dec(device, data, 2)
      
end procedure




procedure _print_suniversal_dec(volatile byte out device, sdword in data, sbyte in divisor_index) is
   
   if (data < 0) then
      data = -data
      device = "-"      
   end if

   _print_universal_dec(device, dword( data ), divisor_index)
      
end procedure

const dword digit_divisor[] = { 1, 10, 100, 1_000, 10_000, 100_000, 1_000_000, 10_000_000, 100_000_000, 1_000_000_000 }
procedure _print_universal_dec(volatile byte out device, dword in data, sbyte in divisor_index) is
   var byte digit
   var bit  no_digits_printed_yet;
   
   if (data == 0) then
      device = "0"      
      return
   end if

   no_digits_printed_yet = true
   while (divisor_index >= 0) loop   
      if (data >= digit_divisor[divisor_index]) then
         digit = byte ( data / digit_divisor[divisor_index] )
         data = data % digit_divisor[divisor_index]  
      else
         digit = 0
      end if
;      digit_divisor = digit_divisor / 10
      divisor_index = divisor_index - 1
      
      if ((digit != 0) | (no_digits_printed_yet == false)) then
         device = digit | "0"
         no_digits_printed_yet = false
      end if
   end loop
      
end procedure
