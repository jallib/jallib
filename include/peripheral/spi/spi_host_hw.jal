-- -----------------------------------------------------------------------------
-- Title: SPI host
-- Author: William Welch Copyright (c) 2009..2025, all rights reserved.
-- Adapted-by: Sebastien Lelong, Rob Jansen
-- Compiler: 2.5r9
--
-- This file is part of jallib (https://github.com/jallib/jallib)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: SPI host hardware control.
--              Routines for sending and receiving through the SPI in host mode
--
-- Sources: Good overview of SPI at http://en.wikipedia.org/wiki/Serial_Peripheral_Interface_Bus
--          Also at: http://elm-chan.org/docs/spi_e.html
--

-- Aliases needed to support newer PICs.
if defined(SPI1CON0) then 
   -- Newer PICs, e.g. PIC18F15Q40.
   alias SPI1BUF_TX      is SPI1TXB
   alias SPI1BUF_RX      is SPI1RXB
   alias SPI1CONTROL     is SPI1CON0
   alias SPI1BUF_SAMPLE  is SPI1CON1_SMP
else
   -- Original version.
   alias SPI1BUF_TX      is SSPBUF
   alias SPI1BUF_RX      is SSPBUF
   alias SPI1STATUS_TXWE is SSPCON1_WCOL
   alias SPI1STATUS_TXBE is SSPSTAT_BF
   alias SPI1CON1_CKE    is SSPSTAT_CKE
   alias SPI1CON1_CKP    is SSPCON1_CKP
   alias SPI1CLK_CLKSEL  is SSPCON1_SSPM
   alias SPI1CONTROL     is SSPCON1
   alias SPI1BUF_SAMPLE  is SSPSTAT_SMP
end if 

include spi_common


-- SPI is full-duplex, so we exchange host and slave data byte.
function spi_host_hw_exchange(byte in m_data) return byte is

   SPI1BUF_TX = m_data

   -- Check for write collision.
   if SPI1STATUS_TXWE then
      return 0xFF
   end if

   -- Wait for transmission to complete.
   while !SPI1STATUS_TXBE loop end loop

   -- Return slave data
   return SPI1BUF_RX

end function


-- Half-duplex convenience function. send data to slave, discard reply.
procedure spi_host_hw'put(byte in data) is

   var byte dummy

   dummy = spi_host_hw_exchange(data)

end procedure


-- Half-duplex convenience function. send 0xFF, get slave data.
function spi_host_hw'get() return byte is

   var byte data

   data = spi_host_hw_exchange(0xFF)

   return data

end function


-- Clock SPI data when SCK goes from low to high. SCK is low inactive low.
procedure spi_host_hw_set_mode_00() is
   pragma inline

   SPI1CON1_CKP = 0
   SPI1CON1_CKE = 1

end procedure


-- Clock SPI data when SCK goes from high to low. SCK is inactive low.
procedure spi_host_hw_set_mode_01() is
   pragma inline

   SPI1CON1_CKP = 0
   SPI1CON1_CKE = 0

end procedure


-- Clock SPI data when SCK goes from high to low. SCK is inactive high.
procedure spi_host_hw_set_mode_10() is
   pragma inline

   SPI1CON1_CKP = 1
   SPI1CON1_CKE = 1

end procedure


-- Clock SPI data when SCK goes from low to high. SCK is inactive high.
procedure spi_host_hw_set_mode_11() is
   pragma inline

   SPI1CON1_CKP = 1
   SPI1CON1_CKE = 0

end procedure


-- Specify SPI mode. See spi_common.jal for predefined constants you can use
-- as parameter.
procedure spi_host_hw_set_mode(byte in spi_mode) is
   pragma inline

   if spi_mode == SPI_MODE_00 then
      spi_host_hw_set_mode_00()
   elsif spi_mode == SPI_MODE_01 then
      spi_host_hw_set_mode_01()
   elsif spi_mode == SPI_MODE_10 then
      spi_host_hw_set_mode_10()
   else
      spi_host_hw_set_mode_11()
   end if
   
end procedure


-- Set the SPI host speed, see spi_common.jal. Some newer PICs have a separate 
-- baud rate register, in that case SCK = Fosc / (2 * (spi_rate + 1)).
procedure spi_host_hw_set_speed(byte in spi_rate) is
   pragma inline

    if defined(SPI1BAUD) then 
      -- Newer PICs have a separate baud rate generator.
      SPI1CLK = 0b0000 -- Use system clock Fosc.
      SPI1BAUD = spi_rate 
   else
      -- Original version. 
      SPI1CLK_CLKSEL = spi_rate
   end if 

end procedure


-- Initialize the SPI registers and enable the SPI interface.
procedure spi_init(byte in spi_mode, byte in spi_rate) is

   -- Disable SPI.
   SPI1CONTROL = 0
   SPI1BUF_SAMPLE = FALSE

   spi_host_hw_set_mode(spi_mode)
   spi_host_hw_set_speed(spi_rate)
   
   if defined(SPI1CON0) then 
      -- Newer PICs.
      SPI1CON0_MST   = TRUE -- SPI host mode. 
      SPI1CON0_BMODE = TRUE -- Apply Bit-Length to every byte.
      SPI1CON2_TXR   = TRUE -- Transmit data required.
      SPI1CON2_RXR   = TRUE -- Receive FIFO space required.
      SPI1CON0_EN    = TRUE
   else
      -- Original version.
      SSPCON1 = 0
      SSPCON1_SSPEN = TRUE
   end if 

end procedure

