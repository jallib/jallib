-- Title: USART2 hardware control
-- Author: Stef Mientki, Copyright (c) 2002..2006, all rights reserved.
-- Adapted-by: Sebastien Lelong.
-- Compiler: >=2.4g
-- Revision: $Revision$
-- 
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: USART2 hardware control.
-- Routines for sending and receiving through the EUSART2
-- (2nd EUSART module)
-- both asynchrone and synchrone are supported.
-- Baudrate can simply be set through a human constant,
-- because the baudrate depending registers are calculated by this unit.
-- Baudrate is calculated, starting at the high baudrate flag,
-- which will ensure the highest possible accuracy.
--
-- TODO: should this be configurable ?
-- Transmission parameters are 8 databits, 1 stopbit, no parity, no handshake.
--

include usart2_common

-- Initializes the serial port, calculates baudrate registers.
procedure serial_hw2_init() is 
   -- Calculate and Load baudrate generator
   _calculate_and_set_baudrate2()
   
   -- disable all USART interrupts
   PIE3_RC2IE = false
   PIE3_TX2IE = false
   
   -- Enable transmitter : TXSTA2_TXEN=1 (preserve TXSTA_BRGH)
   TXSTA2_TXEN = true

   -- Enable serial port : RCSTA2_SPEN=1
   -- Enable receiving   : RCSTA2_CREN=1
   RCSTA2 = 0x90

end procedure


-- Disables USART so ports can be used (temporary) for other purposes.
-- USART can be enabled again by calling serial_hw2_enable()
procedure serial_hw2_disable() is
   -- wait till running transmissions are finished
   while !TXSTA_TRMT loop end loop
   -- Disable Serial port:
   RCSTA2_SPEN = false
end procedure


-- Enables USART
procedure serial_hw2_enable() is
   -- Enable Serial port
   RCSTA2_SPEN = true
end procedure


-- -----------------------------------------------------------------------
-- Write char to serial port, blocking
-- -----------------------------------------------------------------------
-- Asynchronuous serial send routine, using the TX pin
-- Sends byte X (8 bit with no parity) to the serial port
-- First checks (and waits if necessary) if transmit buffer is empty
-- -----------------------------------------------------------------------
procedure serial_hw2_write(byte in data) is
   -- wait until TXREG2 is empty
   while ! PIR3_TX2IF loop end loop
   -- then put new byte in TXREG2 (prepare for transmission)
   TXREG2 = data
end procedure


-- like Serial_H_write, but then with a word as input
-- The MSB is outputed first
procedure serial_hw2_write_word(word in data) is
   var byte DX[2] at data
   -- can be blocked by debugging
   while ! PIR3_TX2IF loop end loop
   TXREG2 = DX[1]
   asm nop  -- this is necessary for damned good optimized compilers
            -- loading of the TXREG2 doesn't immediatly set PIR3_TX2IF !!!!
   while ! PIR3_TX2IF loop end loop
   TXREG2 = DX[0]
end procedure


-- -----------------------------------------------------------------------
-- Internal use only!
-- -----------------------------------------------------------------------
-- (using this inline function for serial_hw2_data'get saves a stack level)
-- Returns true if a character was received, otherwise returns false.
-- Overrun error flag is cleared.
-- -----------------------------------------------------------------------
function _serial_hw2_read(byte out data) return bit is   
   pragma inline
   
   -- test if byte available, and if so,
   -- get byte and transport to outer world
   if PIR3_RC2IF then
      data = RCREG2
      PIR3_RC2IF   = false       -- eur@fiwhex.nl 12-sept-08
   else
      return false  ;result = false
   end if
   
   if RCSTA2_OERR then
      RCSTA2_CREN = false
      RCSTA2_CREN =true
   end if
  
   return true
end function


-- -----------------------------------------------------------------------
-- Read char if available (non-blocking)
-- -----------------------------------------------------------------------   
-- Returns true if a character was received, otherwise returns false.
-- Overrun error flag is cleared.
-- -----------------------------------------------------------------------
function serial_hw2_read(byte out data) return bit is
   return _serial_hw2_read(data)
end function


-- Here Serial read and write are definied as pseudo variables
-- so you use them as normal vars, like
--  * wait for character being received,
--  * then echo the inverted character
-- {{{
-- serial_hw2_data = ! serial_hw2_data
-- }}}
--
-- these procedures will wait till they can perform their action
-- therefore it's better to use to following construct
-- {{{
-- -- if charater received, echo the inverted character
-- if  serial_hw2_data_available then
--     serial_hw2_data = ! serial_hw2_data
-- end if
-- -- do other things
-- }}}
--
procedure serial_hw2_data'put(byte in data) is
   serial_hw2_write(data)
end procedure


function serial_hw2_data'get() return byte is
   var byte data
   while ! _serial_hw2_read(data) loop end loop
   return data
end function


-- -----------------------------------------------------------------------
-- raw interface 
-- -----------------------------------------------------------------------

-- some variables made available under a general (pic-independent) name
alias serial_hw2_data_available is PIR3_RC2IF
alias serial_hw2_data_ready is PIR3_TX2IF

-- These are real raw procedures, declared as pseudo variables
-- the user is totally responsible for testing the transmit/receive
-- flag before using these functions
procedure serial_hw2_data_raw'put(byte in data) is
   TXREG2 = data
end procedure

function serial_hw2_data_raw'get() return byte is
   return RCREG2
end function

