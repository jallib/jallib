-- Title: HT9200 DTMF generator library
-- Author: Matthew Schinkel, copyright (c) 2009, all rights reserved.
-- Adapted-by:
-- Compiler: >=2.4o
--
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: Select a DTMF signal to be generated by HT9200.
--
-- Sources:
--
-- Notes:
-- Sound is only generated while chip select is LOW
--
-- This chip is ment to use 5 bit SPI, so the CS line must be toggled for
-- each data byte sent since the SPI libraries are 8 bit.
--
-- This chip uses SPI mode 1,1 but may work with other modes.
--

-- Tone output frequency table
-- Bits are reversed for LSB spi
const byte hd9200_values[25] = {
0b01010000, ; 0 - 941+1336
0b10000000, ; 1 - 697+1209
0b01000000, ; 2 - 697+1336
0b11000000, ; 3 - 697+1477
0b00100000, ; 4 - 770+1209
0b10100000, ; 5 - 770+1336
0b01100000, ; 6 - 770+1477
0b11100000, ; 7 - 852+1209
0b00010000, ; 8 - 852+1336
0b10010000, ; 9 - 852+1477
0b11010000, ; * - 941+1209
0b00110000, ; # - 941+1477
0b10110000, ; A - 697+1633
0b01110000, ; B - 770+1633
0b11110000, ; C - 852+1633
0b00000000, ; D - 941+1633
0b00001000, ;   - 697
0b10001000, ;   - 770
0b01001000, ;   - 852
0b11001000, ;   - 941
0b00101000, ;   - 1209
0b10101000, ;   - 1336
0b01101000, ;   - 1477
0b11101000, ;   - 1633
0b11111000  ;   - OFF
}

--------------------------------------------------------------------------------
-- Send via SPI without use of chip select lines.
-- sound will play until chip select goes high.
-- See hd9200_values[]
--------------------------------------------------------------------------------
procedure hd9200_send(byte in digit) is
   pragma inline
   spi_master = hd9200_values[digit]
end procedure

--------------------------------------------------------------------------------
-- Send via SPI with use of chip select lines.
-- Length is how long the sound will play in 100ms.
--------------------------------------------------------------------------------
procedure hd9200_digit(byte in digit, byte in length) is
   ht9200_chip_select = LOW -- enable the chip
   spi_master = hd9200_values[digit]
   delay_100ms(length)
   ht9200_chip_select = HIGH -- disable the chip
   delay_1ms(length*20)
end procedure

--------------------------------------------------------------------------------
-- Send a string of numbers to output. Dial a phone number.
-- length is how long the sound will play in 100ms.
-- " " sends no sound.
--------------------------------------------------------------------------------
procedure hd9200_dial(byte in phone_numbers[], byte in length) is
   var word len = count(phone_numbers)
   var byte step1
   for len using step1 loop
      ht9200_chip_select = LOW -- enable the chip
      if phone_numbers[step1] == "*" then
         spi_master = hd9200_values[10]
      elsif phone_numbers[step1] == "#" then
         spi_master = hd9200_values[11]
      elsif phone_numbers[step1] == "A" then
         spi_master = hd9200_values[12]
      elsif phone_numbers[step1] == "B" then
         spi_master = hd9200_values[13]
      elsif phone_numbers[step1] == "C" then
         spi_master = hd9200_values[14]
      elsif phone_numbers[step1] == "D" then
         spi_master = hd9200_values[15]
      elsif phone_numbers[step1] == " " then
         spi_master = hd9200_values[24]
      else
         spi_master = hd9200_values[phone_numbers[step1] - 48]
      end if
      delay_100ms(length)
      ht9200_chip_select = HIGH -- disable the chip
      delay_1ms(length*20)
   end loop
end procedure
