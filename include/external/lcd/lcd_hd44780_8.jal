-- -----------------------------------------------------------------------------
-- Title: LCD library for HD44780 compatible LCDs, with 8 bits wide datatransfer
--
-- Author: Rob Hamerling, Copyright (c) 2008..2009, all rights reserved.
--
-- Adapted-by: Eur van Andel, Joep Suijs (refactoring),Richard Zengerink (lcd_init)
--             Rob Hamerling (issue # 137)
--             Rob Hamerling (issue # 190)
--
-- Compiler: 2.4p
--
-- Revision: $Revision$
--
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Sources:
--
-- Description:
--    Port interface for HD44780 compatible alphanumeric LCD screens.
--    Uses 8 bits wide datapath + 2 handshake lines (total 10 PIC pins).
--    Expects: 2 pins for handshake: 'lcd_rs' and 'lcd_en'
--             and:
--                    1 complete port for data: 'lcd_dataport'
--                 or
--                    2 nibbles for data: 'lcd_dataport_low' and 'lcd_dataport_high'
--                 or
--                    8 individual pins for data: lcd_dataline_d0 .. _d7
-- .
--    note:    a complete ports gives faster and more compact code then
--             two port 'nibble'' (the lower or higher 4 bits of a port)
--             which is on its turn faster than 8 individual pins.
-- .
--    Directions for use of this library in application programs
--    (in this sequence):
--    1. Declare the following constants:
--          const byte LCD_ROWS     = 2               -- 1, 2 or 4 lines
--          const byte LCD_CHARS    = 16              -- 8, 16 or 20 chars per line
--       and aliases for handshake lines:
--          alias   lcd_rs          is  pin_D2        -- cmd/data select
--          alias   lcd_en          is  pin_D3        -- trigger
--       and aliases for 8 data lines:
--             alias  lcd_dataport  is  portF         -- 8 databits
--           or
--             alias  lcd_dataport_low  is portD_high  -- 4 low order databits
--             alias  lcd_dataport_high is portC_high  -- 4 high order databits
--           or
--             alias   lcd_d0       is  pin_D0        -- lcd databit d0
--             alias   lcd_d1       is  pin_D1        -- lcd databit d1
--             alias   lcd_d2       is  pin_C6        -- lcd databit d2
--             alias   lcd_d3       is  pin_C7        -- lcd databit d3
--             alias   lcd_d4       is  pin_B0        -- lcd databit d4
--             alias   lcd_d5       is  pin_B1        -- lcd databit d5
--             alias   lcd_d6       is  pin_C0        -- lcd databit d6
--             alias   lcd_d7       is  pin_C2        -- lcd databit d7
-- .
--    2. Set the chosen LCD handshake pins to output:
--          pin_D2_direction     = output
--          pin_D3_direction     = output
--       and the data port to output:
--              portF_direction      = ALL_OUTPUT
--          or 2 data port nibbles to output:
--              portD_high_direction = ALL_OUTPUT
--              portC_high_direction = ALL_OUTPUT
--          or 8 individual data pins to outout:
--              pin_D0_direction     = output
--              pin_D1_direction     = output
--              pin_C6_direction     = output
--              pin_C7_direction     = output
--              pin_B0_direction     = output
--              pin_B1_direction     = output
--              pin_C0_direction     = output
--              pin_C2_direction     = output
-- .
--    3. Include this library.
-- .
--    4. Call lcd_init() to initialize the lcd controller.
-- .
--    Above is an example for a 2x16 LCD.
-- .
--    See hd_44780_common for the LCD API.
--
-- Dependencies: delay.jal
--               lcd_hd44780_common.jal
--
-- -----------------------------------------------------------------------------

include delay

-- ----------------------------------------------------------------------------
-- Sends byte in <value> to the LCD
-- Can be used for both commands and data.
-- ----------------------------------------------------------------------------
procedure __lcd_write(byte in value) is

   if (defined(lcd_dataport) == TRUE) then
      lcd_dataport = value                      -- copy byte to dataport
   elsif (defined(lcd_dataport_low) == TRUE) then
      lcd_dataport_low  = (value & 0x0F)        -- copy low nibble
      lcd_dataport_high = (value >> 4)          -- copy high nibble
   else
      var bit bit0 at value : 0                 -- )
      var bit bit1 at value : 1                 -- )
      var bit bit2 at value : 2                 -- )
      var bit bit3 at value : 3                 -- )
      var bit bit4 at value : 4                 -- ) declare databits
      var bit bit5 at value : 5                 -- )
      var bit bit6 at value : 6                 -- )
      var bit bit7 at value : 7                 -- )

      lcd_d0 = bit0                             -- )
      lcd_d1 = bit1                             -- )
      lcd_d2 = bit2                             -- )
      lcd_d3 = bit3                             -- )
      lcd_d4 = bit4                             -- ) copy databits
      lcd_d5 = bit5                             -- )
      lcd_d6 = bit6                             -- )
      lcd_d7 = bit7                             -- )
   end if
                                                -- clock pulse
   lcd_en = HIGH                                -- trigger on
   _usec_delay(1)                               -- delay (> 400 ns)
   lcd_en = LOW                                 -- trigger off
   delay_10us(4)                                -- > 37 us
end procedure


-- ----------------------------------------------------------------------------
-- Sends data byte in <value> to LCD
-- ----------------------------------------------------------------------------
procedure _lcd_write_data(byte in value) is

   lcd_rs = high                                -- select data mode
   __lcd_write(value)                           -- write byte

end procedure


-- ----------------------------------------------------------------------------
-- Sends command byte in <value> to LCD
-- For slow commands an extra delay should be added
-- ----------------------------------------------------------------------------
procedure _lcd_write_command(byte in value) is

   lcd_rs = low                                 -- select command mode
   __lcd_write(value)                           -- write byte

end procedure


include lcd_hd44780_common                      -- include common LCD API


-- ----------------------------------------------------------------------------
-- Initialize LCD controller to 8-bits mode and default operational settings
-- ----------------------------------------------------------------------------
procedure lcd_init() is

   lcd_rs = LOW                                 -- set to control char mode
   delay_1ms(25)                                -- power-up delay (> 15 ms)
   _lcd_write_command(0b0011_0000)              -- function set
   delay_1ms(5)                                 -- > 4.1 milliseconds
   _lcd_write_command(0b0011_0000)              -- function set
   delay_10us(10)                               -- > 100 us
   _lcd_write_command(0b0011_0000)              -- function set
   delay_10us(4)                                -- > 37 us
   _lcd_write_command(0b0011_1000)              -- select 8-bits mode and
                                                -- 2 lines, 5x8 dots font
   delay_10us(4)                                -- > 37 us
   _lcd_write_command(0b_0001_1100)             -- cursor (not data) move right
   _lcd_write_command(0b_0000_1100)             -- display on, cursor off, no blink
   _lcd_write_command(0b_0000_0110)             -- cursor shift right, no data shift
                                                -- during read/write operations
   lcd_clear_screen()                           -- clear display

end procedure

