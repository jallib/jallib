-- -----------------------------------------------------------------------------
-- Title: Demo of usb_serial library for the PIC16F1455.
-- Author: Rob Jansen, Copyright (c) 2017..2018, all rights reserved.
-- Adapted-by:
-- Compiler: >= jalv2.4q6
-- Revision: $Revision$
-- 
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
-- 
-- Description: This application demonstratesest USB Serial library, this application 
-- will emulate a RS-232 terminal device. Once the USB device has been detected by the 
-- host operating system, it will create a virtual COM port. When opening this COM
-- on the host computer, using a standard terminal application, it should show a
-- welcome message, after the welcome message all input characters are echoed back
-- to the terminal application.
--
-- Sources: http://www.usb.org for USB specifications.
-- 
-- Notes: compiled with following flags:
-- add -no-variable-reuse when debugging flags are set due to a compiler issue
-- -
-- Some instructions for use:
-- -
-- 1. Make sure you logon as administrator, or with administrator privileges,
--    on Windows 10.
-- 2. If needed convert to the correct PIC device (change include file), 
--    adapt fuses fuses and speed.
-- 3. Create a basic board with USB PIC device, also see below for connections.
-- 4. Compile the this file and program the hex file into the PIC.
-- 5. Windows 10 has a build in USB driver that supports this device.
-- 6. Open the Windows Device Manager and check out the "ports" section. A new
--    communications port will be there, with a COM number.
-- 7. Open a terminal program (for example Termite) and select the COM
--    port you found in the previous step. Pick any baudrate, Windows will
--    accommodate automatically, and use 8-N-1-no for the other settings. Also
--    enable flow control (RTS/CTS) in your terminal program and connect to the
--    device. When connected a welcome message will appear on the screen.
-- 8. Now everything you type will be echoed back.
-- 9. Have fun, adapt to your needs.
--
-- ------------------------------------------------------
--
-- This file was orginally copied from 18f14k50_usb_serial.jal and made
-- operational to work for the PIC16F1454/55/59 using the modified usb driver
-- as to function without the availability of a POSTINC1 feature which is not
-- present in these PICs.
--

;@jallib section chipdef
-- chip setup
include 16f1455                  -- Target processor.

;@jallib section board_description
-- For hardware a 14 pin DIL PIC16F1455 was used. The circuit can be powered by
-- the USB connection and connected as follows:
-- *) USB data- wire to pin 12 (D-)
-- *) USB data+ wire to pin 13 (D+)
-- *) USB power wire to pin 1 (Vdd). Don't forget 100nF decoupling capacitor.
-- *) USB ground wire to pin 14 (Vss)
-- *) A resistor of 33kOhm to pin 4 (MCLR) and power. We use internal reset.
-- *) A capacitor of 220nF to pin 11 (Vusb3v3) and ground.
-- *) A LED with 330 Ohm resistor to pin 7. Indicates that USB is configured.
-- *) A crystal of 12 MHz to pin 2 and pin 3 with 2*18 pF capacitors to ground.
-- Settings are also given to use the internal oscilator of the PIC so no need
-- for the 12 MHz crystal which saves you a crystal and 2 pins on the chip.
-- The configuration runs at an internal clock of 48 MHz.
-- For debugging purposes the USART is used to send information (pin 5)

-- Some info on timing. With an 48 MHz clock the main loop takes about
-- 9.5 us of which 3 us is used by 'usb_serial_flush' which is about 30%
-- slower than a PIC that has the POSTINC1 feature but still sufficiently fast.

pragma target clock       48_000_000

-- Compiler options.
Pragma warn all no              -- We do not want to know all compiler warnings
Pragma opt variable_reduce yes

-- fuses
-- Settings for external 12 MHz crystal and system clock 48 MHz.
pragma target OSC           HS       -- External crystal
pragma target CLKOUTEN      ENABLED  -- CLKOUT function is enabled
pragma target PLLMULT       N4X      -- PLL Multipler Selection Bit, 4x Output Frequency Selected

-- Settings for internal clock and system clock 48 MHz.
-- pragma target OSC           INTOSC_NOCLKOUT -- Internal clock
-- pragma target CLKOUTEN      DISABLED -- CLKOUT function is disabled. I/O or oscillator function on the CLKOUT pin
-- pragma target PLLMULT       N3X      -- PLL Multipler Selection Bit, 3x Output Frequency Selected

-- Other fuses
pragma target CPUDIV        P1       -- NO CPU system divide
pragma target USBLSCLK      F48MHZ   -- System clock expects 48 MHz, FS/LS USB CLKENs divide-by is set to 8.
pragma target PLLEN         ENABLED  -- 3x or 4x PLL Enabled
pragma target FCMEN         DISABLED -- Fail-Safe Clock Monitor is disabled
pragma target WRT           DISABLED -- Write protection off
pragma target STVR          ENABLED  -- Stack Overflow or Underflow will cause a Reset
pragma target LPBOR         DISABLED -- Low-Power BOR is disabled
pragma target IESO          DISABLED -- Internal/External Switchover Mode is disabled
pragma target PWRTE         DISABLED -- power up timer
pragma target BROWNOUT      DISABLED -- no brownout detection
pragma target WDT           DISABLED -- Watchdog disabled
pragma target MCLR          Internal -- Internal reset
pragma target LVP           DISABLED -- no low-voltage programming
pragma target VOLTAGE       MAXIMUM  -- brown out voltage
pragma target CP            DISABLED -- Program memory code protection is disabled

OSCCON        = 0b1111_1100          -- Select PLL,3x, 16MHz internal oscillator

-- Enable weak pull-up for port a and and set port c to output just to
-- have no floating input pins.
OPTION_REG_WPUEN = FALSE             -- Enable weak pull-up for port a.
WPUA          = 0b0011_1000          -- Weak-pull up for relevant port a bits.
TRISC         = 0b0000_0000          -- Port c output.

Alias Configured_LED Is pin_c3       -- Pin 7.
pin_c3_direction =  Output

-- For debugging purposes only: Set debug mode and set variables for debug. See
-- usb_serial.jal to see which options exist. Note that enabling all debugging
-- will prevent the driver from correct operation.
-- const USB_CDC_DEBUG = 1
-- alias pin_RX_direction is pin_RX_DT_direction
-- alias pin_TX_direction is pin_TX_CK_direction
-- const serial_hw_baudrate = 115_200
-- include serial_hardware
-- serial_hw_init()

-- Include the rest
include print
include usb_serial

-- constants
const byte str_welcome[] = "JALLIB USB Serial Demo app\n"

-- interrupts? No thanks
INTCON_GIE = false

-- setup the USB serial library
usb_serial_init()

-- variables
var bit has_shown_welcome_msg = true
var byte ch

-- main loop
forever loop
	-- poll the usb ISR function on a regular base, in order to
	-- serve the USB requests
	usb_serial_flush()

    -- check if USB device has been configured by the HOST
	if ( usb_cdc_line_status() !=  0x00 )  then
		if !has_shown_welcome_msg then
			has_shown_welcome_msg = true
			print_string( usb_serial_data, str_welcome )
		end if	
    Configured_LED = TRUE	
	else
		has_shown_welcome_msg = false
    Configured_LED = FALSE	
	end if

	-- check for input character
	if usb_serial_read( ch ) then
		-- echo input character
		usb_serial_data = ch
	end if
	
end loop
