-- -----------------------------------------------------------------------------
-- Title: Receiver test program for testing a Virtual Wire connection.
--
-- Author: Rob Jansen, Copyright (c) 2018..2019, all rights reserved.
--
-- Adapted-by:
--
-- Compiler: 2.5r2
--
-- Revision: $Revision$
--
-- This file is part of jallib (https://github.com/jallib/jallib)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Sources: http://www.airspayce.com/mikem/arduino/VirtualWire.pdf
--
-- Description: This test program receives a message via a Virtual Wire
--              connection using a standard 433 MHz RF receiver module.
--
-- Dependencies: -
--
-- Notes: Do not enable weak-pull up for the pin to which the receiver is
--        connected.
--
-- -----------------------------------------------------------------------------

-- Some compiler pragmas for optimizations
pragma warn all no             -- We do not want to know all compiler warnings
pragma opt variable_reduce yes -- Reduce variables.

include 16f1825              -- target PICmicro

-- This program assumes no crystal but uses the 32 MHz internal oscillator
pragma target clock 32_000_000     -- oscillator frequency 32 MHz

-- Configuration memory settings (fuses)
pragma target OSC      INTOSC_NOCLKOUT -- Internal Clock
pragma target PWRTE    enabled         -- Power up timer
pragma target MCLR     internal        -- reset internal
pragma target PLLEN    enabled         -- PLL Enable
pragma target WDT      disabled        -- no watchdog
pragma target LVP      disabled        -- no Low Voltage Programming

-- Set the internal clock frequency to 32 MHz
OSCCON_IRCF   = 0b1110  -- Set 32 MHz (uses 8 MHz source)
OSCCON_SCS    = 0b00    -- Clock determined by FOSC (32 MHz)

-- ------------------------------ Pins -----------------------------------------
-- Enable weak pull up for all unused ports since some inputs are not connected
WPUA = 0b0011_0011        -- Weak pull-up for RA0, RA1, RA4, RA5
WPUC = 0b0011_1011        -- Weak pull-up for RC0, RC1, RC3, RC4, RC5, not RC2!
OPTION_REG_WPUEN = FALSE  -- Enable Weak Pull-Up

-- Declaration of the virtual wire receiver pin
alias vw_rx_pin is pin_c2   -- Pin 8. Note. Do not enable weak-pull up.
pin_c2_direction = input

-- Dummy declaration of the virtual wire transmitter pin. Only needed when using
-- the full virtual wire library.
alias vw_tx_pin is pin_c1
pin_c1_direction = output

-- LED indicating transmission of the message. Connect via 330 Ohm resistor.
-- High is LED on.
alias led is pin_a2          -- Pin 11.
pin_a2_direction = output

-- Input pin that determined the bitrate. High is high bitrate.
alias high_bitrate is pin_c0      -- Pin 10.
pin_c0_direction = input

-- Declaration of the USART pins
alias pin_RX_direction is pin_RX_RC5_direction  -- Pin 5.
alias pin_TX_direction is pin_TX_RC4_direction  -- Pin 6.

-- ------------------- Constant and variable declarations ----------------------
const byte VW_MESSAGE_BUFFER  = 25   -- Receive buffer is vw_rx_return_buf
const serial_hw_baudrate      = 9600

-- Selection of bitrates.
const byte VW_SPEED_1000 = 250 -- 1000 bits/second
const byte VW_SPEED_2000 = 125 -- 2000 bits/second

var   byte how_long
var   byte index

include virtual_wire         -- Full driver version
--include virtual_wire_receiver   -- Receiver only version. Saves memory.

-- --------------------- Functions and Procedures ------------------------------
-- These two procedures are needed for using the virtual wire library. The
-- timer can be any timer of the PIC.
procedure timer2_setup(byte in bitrate) is
  -- The timer runs at Fosc/4 so 32.000.000 Hz --> 8 MHz.
  -- If we prescale by 4 then it runs at 2 MHz. With a bit time of 1 ms
  -- (1000 bit/s) and a sample time of VW_RX_SAMPLES_PER_BIT (=8) gives
  -- 2 MHz / 250 = 8 kHz or 125 us. For 2000 bits/s the reload value has to
  -- be 125 (gives 16 kHz divided by 8 samples gives 2000 bits/s)
  T2CON_T2CKPS  = 0b01   -- Prescale = 4
  T2CON_T2OUTPS = 0b0000 -- Postcale = 0
  PR2 = bitrate          -- Compare value
  PIE1_TMR2IE   = TRUE   -- Enable Timer 2 interrupt
  T2CON_TMR2ON  = TRUE   -- Start Timer
  -- Enable all interrupts.
  INTCON_PEIE = TRUE     -- Peripheral interrupts enabled.
  INTCON_GIE  = TRUE     -- Globale interrupt enabled
end procedure

procedure timer2_interrupt() is pragma interrupt
  -- Timer procedure. Call the virtual wire serviice routine if needed.
  if PIR1_TMR2IF then
     PIR1_TMR2IF = FALSE
     -- Call the virtual wire service routine.
     vw_service()
  end if
end procedure

-- ------------------------------ Main Program  --------------------------------
include serial_hardware
enable_digital_io() -- Make all pins digital I/O
serial_hw_init()
vw_init()
-- Setup the timer and the bitrate.
if high_bitrate then
  timer2_setup(VW_SPEED_2000)         -- High bitrate.
else
  timer2_setup(VW_SPEED_1000)         -- Low bitrate
end if
vw_rx_start()          -- Start the receiver pll running.

led = false

forever loop
  how_long = VW_MESSAGE_BUFFER -- Maximum number of bytes to copy.
  if vw_get_message(how_long) then
    led = true
    -- Variable 'how_long' now holds the actual number of received bytes.
    for how_long using index loop
       serial_hw_write(vw_rx_return_buf[index])
    end loop
    led = false
  end if
end loop





