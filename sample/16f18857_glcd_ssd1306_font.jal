-- -----------------------------------------------------------------------------
-- Title: GLCD SSD1306 demo sample showing text (fonts).
-- Author: Rob Jansen, Copyright (c) 2026..2026, all rights reserved.
-- Adapted-by:
-- Compiler: 2.5r9
--
-- This file is part of jallib (https://github.com/jallib/jallib)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: Demo program showing how to use the OLED display using an SSD1306. 
--              It demonstrates tje features of the glcd_ssd1306.jal library. 
--              This sample shows the text (font usage). 
--
-- Notes:  For a text and graphics version see the sample file
--         16f18857_glcd_ssd1306_font_graphics.jal.
--

include 16f18857

-- This program uses the internal oscillator at 16 MHz (PIC at 3.3V)
pragma target clock    16_000_000 -- oscillator frequency
pragma target OSC      OFF        -- internal oscillator
pragma target RSTOSC   HFINT32    -- select 32 MHz
pragma target CLKOUTEN DISABLED   -- no clock output
pragma target WDT      DISABLED   -- watchdog
pragma target BROWNOUT DISABLED   -- no brownout reset
pragma target FCMEN    DISABLED   -- no clock monitoring
pragma target CSWEN    ENABLED    -- allow writing OSCCON1 NOSC and NDIV
pragma target LVP      ENABLED    -- low voltage programming
pragma target MCLR     EXTERNAL   -- external reset
OSCFRQ_HFFRQ = 0b101              -- Fosc 16 MHz

enable_digital_io()
-- Wait some time for the hardware to power up.
_usec_delay(100_000)

-- The default display resolution is 128 x 64. When using 128 x 32 define:
-- const SSD1306_Y_32_PIXELS = TRUE

-- In this sample program you can select between IIC or SPI communication.
const SSD1306_USE_IIC = TRUE -- When commenting this out, the program will use SPI.
if defined(SSD1306_USE_IIC) then
   -- Using IIC, include and initialize IIC.
   const word _i2c_bus_speed = 4 -- 400kHz (set to 1 gives 100 kHz) 
   const bit _i2c_level = TRUE   -- i2c levels (not SMB)
   alias i2c_scl is pin_C3                     -- To SCK of SSD1306
   alias i2c_scl_direction is pin_C3_direction -- Pin 14 of 28 pin DIP
   alias i2c_sda is pin_C4                     -- To SDA of SSD1306
   alias i2c_sda_direction is pin_C4_direction -- Pin 15 of 28 pin DIP  
   include i2c_hardware
   i2c_initialize()
else
   -- Using SPI.
   ; Define the required SPI pins.
   alias ssd1306_sck is pin_C3                     -- To D0 of SSD1306 
   alias ssd1306_sck_direction is pin_C3_direction -- Pin 14 of 28 pin DIP 
   alias ssd1306_sdo is pin_C4                     -- To D1 of SSD1306  
   alias ssd1306_sdo_direction is pin_C4_direction -- Pin 15 of 28 pin DIP   
   alias ssd1306_csn is pin_C2                     -- To CS of SSD1306  
   alias ssd1306_csn_direction is pin_C2_direction -- Pin 13 of 28 pin DIP  
   alias ssd1306_dc is pin_C1                      -- To DC of SSD1306 
   alias ssd1306_dc_direction is pin_C1_direction  -- Pin 12 of 28 pin DIP     
   -- The SPI version of the SSD1306 uses a hardware reset pin for its reset. 
   -- This optional feature can be used by defining the following:
   alias ssd1306_res is pin_c0                      -- To RES of SSD1306 
   alias ssd1306_res_direction is pin_c0_direction  -- Pin 11 of 28 pin DIP 

   -- You can use software SPI too by setting this constant.
   -- const SSD1306_SOFTWARE_SPI = TRUE 
   if !defined(SSD1306_SOFTWARE_SPI) then
      -- Using SPI hardware and initiallize.
      include spi_master_hw
      spi_init(SPI_MODE_00, SPI_RATE_FOSC_16)
   end if 
end if 

-- Set the pinning for the hardware pins of IIC or SPI. This is default but 
-- still needed for this chip. Do not use when using software SPI since it
-- the pins are then assigned for hardware SPI.
if !defined(SSD1306_SOFTWARE_SPI) then
   include pps
   pps_control_lock(FALSE)                
   RC4PPS = PPS_SDA1  -- SDA re-assigned to C4  (default)             
   RC3PPS = PPS_SCK1  -- SCK re-assigned to C3  (default)            
   pps_control_lock(TRUE)                 
end if

include delay
include print

-- Font libraries.
include glcd_6x8_font
include glcd_12x16_font
-- Note that we do not need a character buffer for the horizontal font since
-- graphics is used and so the character is written using pixel writing.
const byte SSD1306_CHAR_BUFFER_SIZE = FONT_12X16_BYTE_PER_CHAR
-- Only use fonts, no graphics.
const SSD1306_TEXT_ONLY = TRUE
include glcd_font

-- Include and initialize the GLCD lib. This will also initialize the IIC or
-- SPI interface.
include glcd_ssd1306
ssd1306_init()

-- Show text normal
procedure show_normal() is
   glcd_background_color = GLCD_WHITE
   glcd_pen_color = GLCD_BLACK
end procedure 

-- Show text inverse
procedure show_inverse() is
   glcd_background_color = GLCD_BLACK
   glcd_pen_color = GLCD_WHITE
end procedure 

-- Variable declaration.
var byte x

forever loop
 
   -- First write the demo text.
   show_normal()
   ssd1306_clear_screen()
   glcd_font_use(FONT_6X8)
   ssd1306_goto(22,0)
   print_string(glcd,"JAL SSD1306")  
   ssd1306_goto(20,1)
   print_string(glcd,"Library Demo")
   ssd1306_goto(30,4)
   print_string(glcd,"2026-01-25")
   ssd1306_goto(30,5)
   print_string(glcd,"Text only")
   ssd1306_goto(32,6)
   print_string(glcd,"Font 6x8")
   delay_1s(3)

   -- Goto test. Show all corners.
   glcd_font_use(FONT_6X8)
   -- Top left.
   ssd1306_goto(0,0)
   glcd = "8"
   -- Top utmost right for this font size.
   ssd1306_goto(122,0) -- Font is 6 wide so places it to the most right position.
   glcd = "8"          -- at 122 .. 127.
   -- Bottom left.
   ssd1306_goto(0,7) 
   glcd = "8"
   -- Bottom utmost right for this font size.
   ssd1306_goto(122,7) -- Font is 6 wide so places it to the most right position.       
   glcd = "8"          -- at 122 .. 127.
   delay_1s(3)

   -- Show bigger horizontal font
   ssd1306_clear_screen()
   ssd1306_goto(0,0)
   glcd_font_use(FONT_12X16)
   print_string(glcd,"Font 12*16 with text" )

   -- And inverted
   show_inverse()
   ssd1306_goto(0,3)
   print_string(glcd,"Font 12*16" )
   delay_1s(3)

   -- Continue with 6X8 font.
   show_normal()
   ssd1306_clear_screen()
   ssd1306_goto(0,0)
   glcd_font_use(FONT_6X8)
   print_string(glcd,"Font 6*8")
   delay_1s(1)

   ssd1306_goto(0,3)
   -- we'll print numbers starting with "0" in inverted format.
   x = 48
   -- numbers are printed inverted
   show_inverse()
   while x <= 57 loop
      glcd = x
      delay_100ms(1)
      x = x + 1
   end loop

   ssd1306_goto(0,4)
   -- now print capital letters and small letters in normal format.
   x = 65  -- this is "A"
   -- not inverted (ie. normal)
   show_normal()
   while x <= 122 loop
      glcd = x
      delay_100ms(1)
      x = x + 1
   end loop
   delay_1s(3)

   ssd1306_goto(0,0)
   print_string(glcd,"Brightness Demo")

   -- Play with the contrast.
   for 255 using x loop
      ssd1306_set_contrast(x)
      _usec_delay(20_000)
   end loop

   -- Show some display features.
   ssd1306_goto(0,0)
   print_string(glcd,"All Segments On   ")
   delay_1s(3)
   ssd1306_display_all_on()
   delay_1s(3)
   ssd1306_goto(0,0)
   print_string(glcd,"Resume Display    ")
   ssd1306_display_resume()
   delay_1s(3)
   ssd1306_goto(0,0)
   print_string(glcd,"Inverse Display   ")
   ssd1306_display_inverse()
   delay_1s(3)
   ssd1306_goto(0,0)
   print_string(glcd,"Normal Display    ")
   ssd1306_display_normal()
   delay_1s(3)
   ssd1306_goto(0,0)
   print_string(glcd,"Display Off and On")
   delay_1s(2)
   ssd1306_display_off()
   delay_1s(2)
   ssd1306_display_on()
   delay_1s(2)
  
   -- Show some display scroll features.
   ssd1306_goto(0,0)
   print_string(glcd,"Scroll Right...   ")
   ssd1306_goto(0,1)
   print_string(glcd,"...and Left       ")
   delay_1s(2)
   ssd1306_start_scroll_right(0x00, 0x07)
   delay_1s(8)
   ssd1306_stop_scroll()
   delay_1s(1)
   ssd1306_start_scroll_left(0x00, 0x07)
   delay_1s(8)
   ssd1306_stop_scroll()
   delay_1s(2)

   -- Custom made vertical scroll. There is no automatic vertical scroll
   -- feature supported by the SSD1306 but we can use the set start line 
   -- function to create one.
   ssd1306_goto(0,0)
   print_string(glcd,"Scroll Up ...     ")
   ssd1306_goto(0,1)
   print_string(glcd,"...and Down       ")
   delay_1s(2)
   -- Up.
   for SSD1306_MAX_Y_PIXELS using x loop
     ssd1306_set_start_line(x)
     delay_1ms(50)
   end loop
   -- Down.
   delay_1s(3)
   x = SSD1306_MAX_Y_PIXELS -1 
   for SSD1306_MAX_Y_PIXELS loop
      ssd1306_set_start_line(x)
      x = x - 1
      delay_1ms(50)
   end loop
   delay_1s(2)
   -- Back to normal.
   ssd1306_set_start_line(0) 

   -- Note. This only seems to work for the SSD1306 with SPI interface.
   ssd1306_goto(0,0)
   print_string(glcd,"Scroll Diagonal...")
   ssd1306_goto(0,1)
   print_string(glcd,"...Right and Left ")
   delay_1s(2)
   ssd1306_start_scroll_diagonal_right(0x00, 0x07)
   delay_1s(5)
   ssd1306_stop_scroll()
   delay_1s(1)
   ssd1306_start_scroll_diagonal_left(0x00, 0x07)
   delay_1s(5)
   ssd1306_stop_scroll()
   delay_1s(3)

end loop


